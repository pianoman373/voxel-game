#pragma BEGINVERTEX

#include "cubemapVertex.glsl"

#pragma ENDVERTEX



#pragma BEGINFRAGMENT

out vec4 outColor;

in vec3 fPosition;

uniform samplerCube environmentMap;
uniform float roughness;

#include "PBR.glsl"
#include "lighting.glsl"

void main()
{
    
    vec3 N = normalize(fPosition);
    vec3 R = N;
    vec3 V = R;

    const uint SAMPLE_COUNT = 2048u;
    float totalWeight = 0.0;
    vec3 prefilteredColor = vec3(0.0);
    for(uint i = 0u; i < SAMPLE_COUNT; ++i)
    {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H  = ImportanceSampleGGX(Xi, N, roughness);
        vec3 L  = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(dot(N, L), 0.0);
        if(NdotL > 0.0)
        {
            prefilteredColor += texture(environmentMap, L).rgb * NdotL;
            totalWeight      += NdotL;
        }
    }
    prefilteredColor = prefilteredColor / totalWeight;

    outColor = vec4(prefilteredColor, 1.0);
}

#pragma ENDFRAGMENT